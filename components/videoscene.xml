<?xml version = "1.0" encoding = "utf-8" ?> 

<!--********** Copyright 2016 Roku Corp.  All Rights Reserved. **********-->

<component name = "VideoExample" extends = "Scene" >
  <script type = "text/brightscript" >

    <![CDATA[

    sub init()
      m.assetUrl = "http://ais.channel4.com/asset/4058897?client=roku"
      getLicenseUrl()
    end sub


    function onPositionChanged()
      m.progressBar.duration = m.player.duration
      m.progressBar.position = m.player.position
    end function

    function onPlayerStateChanged()
      if m.player.state = "error" then
        print "error: " + m.player.errorMsg
        print "error: " + m.player.errorCode.toStr()
      end if
    end function

    function onKeyEvent(key as String, press as Boolean) as Boolean
      showPlayerBarOverlay()
    end function

    sub showPlayerBarOverlay()
      m.playerBarOverlay.visible = true
      startPlayerBarOverlayTimer()
    end sub

    sub hidePlayerBarOverlay()
      m.playerBarOverlay.visible = false
    end sub

    sub startPlayerBarOverlayTimer()
      if m.playerBarOverlayTimer = invalid then
          m.playerBarOverlayTimer = m.top.findNode("playerBarOverlayTimer")
      end if
      m.playerBarOverlayTimer.ObserveField("fire", "onPlayBarOverlayTimedOut")
      m.playerBarOverlayTimer.control = "start"
    end sub

    sub onPlayBarOverlayTimedOut()
      hidePlayerBarOverlay()
      if m.playerBarOverlayTimer = invalid then
          m.playerBarOverlayTimer = m.top.findNode("playerBarOverlayTimer")
      end if
      m.playerBarOverlayTimer.control = "stop"
    end sub

    sub getLicenseUrl()
      print ".......getLicenseUrl()......."

      m.videoLicenseDataService = createObject("roSGNode", "videoDataService")
      m.videoLicenseDataService.observeField("resultData", "licenseUrlFetched")
      
      print "............... assetUrl = ", m.assetUrl
      m.videoLicenseDataService.functionName = "getAssetVideoStreamUrl"
      m.videoLicenseDataService.assetUrl = m.assetUrl
      m.videoLicenseDataService.control = "RUN"
    end sub

    sub licenseUrlFetched()
        print ".......licenseUrlFetched()......."
        resultData = m.videoLicenseDataService.resultData

        'reset the thread as soon as we are done working with it.
        m.videoLicenseDataService.control = "DONE"
        m.videoLicenseDataService = invalid

        print resultData

         'throws back the error to playerwidget.
        if resultData.isError = invalid then

            m.player = m.top.findNode("exampleVideo")
            m.playerBarOverlay = m.top.findNode("playerBarOverlay")
            m.progressBar = m.top.findNode("progressBar")
            m.player.observeField("state", "onPlayerStateChanged")
            m.player.observeField("position", "onPositionChanged")

            videocontent = createObject("RoSGNode", "ContentNode")

            videoContent.title = "Example Video"
            'videoContent.videoDisableUI = true
            videoContent.encodingType =  "PlayReadyLicenseAcquisitionUrl"
            
            videoContent.encodingKey = resultData.licenseurl
            videoContent.url = resultData.streamuri
            videoContent.streamformat = "ism"

            m.player.content = videocontent

            m.player.setFocus(true)
            m.player.control = "play"
        end if
    end sub
    ]]>

  </script>

  <children >

    <Video id = "exampleVideo"/>
    
    <Rectangle id="playerBarOverlay" visible="false" width="1280" height="160" translation="[0, 560]" opacity="0.90" color="#1E2226">
            
                <!--<Poster id = "poster" visible="true" uri="pkg:/images/welcome.png"/>-->
            <progressBarWidget id="progressBar" translation="[202,72]" />
    </Rectangle>


    <Timer id="playerBarOverlayTimer" repeat="true" duration="5"/>
  </children>

</component>
